-- @path Palladio=http://palladiosimulator.org/PalladioComponentModel/5.2
-- @path JaMoPP=http://www.emftext.org/java/

module Palladio2JaMoPP;
-- Module Template
create OUT : JaMoPP from IN: Palladio;

rule Repostiory2Package {
	from R : Palladio!Repository
	to PM : JaMoPP!Package (
		name <- 'MediaStore'
	),
	PI : JaMoPP!Package (
		name <- 'MediaStore.' + R.entityName + 'Contracts',
		compilationUnits <- R.interfaces__Repository->collect(e | thisModule.OperationInterface2CompilationUnit(e))
		--imports <- R.components__Repository --how to create sub-Packages?!
	),
	PD : JaMoPP!Package (
		name <- 'MediaStore.' + R.entityName + 'DataTypes',
		compilationUnits <- R.dataTypes__Repository
	),
	PC : JaMoPP!Package (
		name <- 'MediaStore.' + R.entityName--,
		--compilationUnits <- R.components__Repository --each their own package instead - seperate, but with a corresponding name
	)
}



--anything better than class? TypeParameter doesn't work, as it is no concrete classifier
unique lazy rule DataType2Class {
	from D : Palladio!DataType
	to T : JaMoPP!Class (
		name <- D.entityName
		--no transformable content - Palladio DataType doesn't inherit from anything and has its repository as only attribute
	)
}

--Interface Foo
unique lazy rule OperationInterface2CompilationUnit {
	from B : Palladio!OperationInterface
	to CU : JaMoPP!CompilationUnit (
		name <- B.entityName,
		classifiers <- thisModule.OperationInterface2Interface(B)
	)
}

unique lazy rule OperationInterface2Interface {
	from O : Palladio!OperationInterface
	to I : JaMoPP!Interface (
		name <- O.entityName,
		members <- O.signatures__OperationInterface->collect(e | thisModule.Signature2ClassMethod(e))
		--typeParameters <- O.signatures__OperationInterface->collect(e | thisModule.Signature2TypeParameter(e))
	)
}

unique lazy rule Signature2ClassMethod{
	from S : Palladio!Signature
	to O : JaMoPP!ClassMethod (
		name <- S.entityName,
		--returnType so nur möglich, weil im Modell alle returnTypes von signaturen Composite-Datatypes sind, sonst if-abfrage nötig, weil kein parent typ konkret genug ist
		--if-abfrage mit dirty-hack notwendiges übel, weil sonst "NullPointer" (bzw. OclUndefined) fehler auftreten, wenn kein returnType existiert
		typeReference <- 
		if not S.returnType__OperationSignature->oclIsUndefined()
			then thisModule.CompositeDataType2ClassifierReference(S.returnType__OperationSignature)
			else Set{}
		endif,
		parameters <- S.parameters__OperationSignature->collect(e | thisModule.Parameter2Parameter(e))
	)
}

unique lazy rule Parameter2Parameter {
	from P1 : Palladio!Parameter
	to P2 : JaMoPP!OrdinaryParameter (
		name <- P1.parameterName,
		--so nur möglich, weil im Modell alle Parameter Typen Composite-Datatypes sind, sonst if-abfrage nötig, weil kein parent typ konkret genug ist
		typeReference <- thisModule.CompositeDataType2ClassifierReference(P1.dataType__Parameter)
	)
}

unique lazy rule Signature2TypeParameter {
	from S :  Palladio!OperationSignature
	to T :  JaMoPP!TypeParameter (
		name <- S.entityName,
		extendTypes <- Set {S.parameters__OperationSignature}
	)
}
--Interface Foo end

--Component Foo
rule BasicComponent2Package {
	from B : Palladio!BasicComponent
	to CU : JaMoPP!Package (
		name <- 'MediaStore.' + B.repository__RepositoryComponent.entityName + '.' + B.entityName,
		compilationUnits <- thisModule.BasicComponent2CompilationUnit(B)
	)
}

lazy rule BasicComponent2CompilationUnit {
	from B : Palladio!BasicComponent
	to CU : JaMoPP!CompilationUnit (
		name <- B.entityName + 'Impl',
		classifiers <- thisModule.BasicComponent2Class(B)
	)
}

unique lazy rule BasicComponent2Class {
	from B : Palladio!BasicComponent
	to C : JaMoPP!Class(
		name <- B.entityName + 'Impl',
		implements <- B.providedRoles_InterfaceProvidingEntity,
		members <- B.requiredRoles_InterfaceRequiringEntity->collect(e | thisModule.RequiredRole2Field(e)),
		members <- thisModule.BasicComponent2Constructor(B),
		members <- B.requiredRoles_InterfaceRequiringEntity->collect(e | thisModule.RequiredRole2Getter(e)),
		members <- B.requiredRoles_InterfaceRequiringEntity->collect(e | thisModule.RequiredRole2Setter(e))
	)
}

	--Component Member Foo
lazy rule RequiredRole2Field {
	from R : Palladio!OperationRequiredRole
	to F : JaMoPP!Field(
   		name <- 'required_' + R.entityName,
		typeReference <- thisModule.OperationInterface2ClassifierReference(R.requiredInterface__OperationRequiredRole)
	)
}
	
lazy rule BasicComponent2Constructor {
	from B : Palladio!BasicComponent
	to C : JaMoPP!Constructor (
		name <- B.entityName + 'Impl',
		parameters <- B.requiredRoles_InterfaceRequiringEntity->collect(e | thisModule.OperationInterface2Parameter(e.requiredInterface__OperationRequiredRole))
	)
}

		--Member Ref. to Interface
lazy rule OperationInterface2ClassifierReference {
	from I : Palladio!OperationInterface
	to R : JaMoPP!ClassifierReference (
		target <- thisModule.OperationInterface2Interface(I)
	)
}

lazy rule RequiredRole2Getter {
	from R : Palladio!OperationRequiredRole
	to G: JaMoPP!ClassMethod(
		name <- 'get' + R.entityName,
		typeReference <- thisModule.OperationInterface2ClassifierReference(R.requiredInterface__OperationRequiredRole)
	)
}

lazy rule RequiredRole2Setter {
	from R : Palladio!OperationRequiredRole
	to S: JaMoPP!ClassMethod(
		name <- 'set' + R.entityName,
		parameters <- thisModule.OperationInterface2Parameter(R.requiredInterface__OperationRequiredRole)
	)
}

		--Member Ref. to Interface
lazy rule OperationInterface2Parameter {
	from I : Palladio!OperationInterface
	to P : JaMoPP!OrdinaryParameter (
		name <- 'required_' + I.entityName,
		typeReference <- thisModule.OperationInterface2ClassifierReference(I)
	)
}

rule ProvidedRole2ClassifierReference {
	from P : Palladio!OperationProvidedRole
	to T : JaMoPP!ClassifierReference(
		target <- thisModule.BasicComponent2Class(P.providingEntity_ProvidedRole)
	)
}
	--Component Member Foo end
--Component Foo end

--CompositeDataType Foo
rule CompositeDataType2CompilationUnit {
	from B : Palladio!CompositeDataType
	to CU : JaMoPP!CompilationUnit (
		name <- B.entityName,
		classifiers <- thisModule.CompositeDataType2Class(B)
	)
}

unique lazy rule CompositeDataType2Class { --? wie getter und setter einrichten? TODO
	from D : Palladio!CompositeDataType
	to C : JaMoPP!Class (
		name <- D.entityName,
		members <- D.innerDeclaration_CompositeDataType --Java Class erbt von Member UND MemberContainer
	)	
}

rule InnerDeclaration2Class { --? kaum inhalte in inner declaration, wie soll ich daraus eine sinnvolle klasse machen? TODO
	from I : Palladio!InnerDeclaration
	to C : JaMoPP!Class (
		name <- I.entityName
	)
}

lazy rule CompositeDataType2ClassifierReference {
	from C : Palladio!CompositeDataType
	to R : JaMoPP!ClassifierReference (
		target <- thisModule.CompositeDataType2Class(C)
	)
}
--CompositeDataType Foo end